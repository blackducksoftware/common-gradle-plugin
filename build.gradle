import java.time.Duration
import java.net.URI

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'signing'
    id 'eclipse'
    id 'com.jfrog.artifactory' version '4.10.0'
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'io.codearte.nexus-staging' version '0.21.1'
    id 'de.marcphilipp.nexus-publish' version '0.4.0'
}

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
tasks.withType(GroovyCompile) { options.encoding = 'UTF-8' }

group = 'com.blackducksoftware.integration'
version = '0.0.85'
description = 'A common plugin for all integration libraries and solutions to use.'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    maven { url "${project.ext.downloadArtifactoryUrl}/${project.ext.artifactoryReleaseRepo}"}
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2/' }
}

dependencies {
    implementation gradleApi()

    //https://www.jfrog.com/confluence/display/RTF/Gradle+Artifactory+Plugin
    //https://github.com/JFrogDev/build-info/tree/master/build-info-extractor-gradle
    implementation 'org.jfrog.buildinfo:build-info-extractor-gradle:4.10.0'

    //https://github.com/hierynomus/license-gradle-plugin
    implementation 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0'

    //https://github.com/kt3k/coveralls-gradle-plugin
    implementation 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'

    //https://github.com/Codearte/gradle-nexus-staging-plugin
    implementation 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.1'

    //https://github.com/marcphilipp/nexus-publish-plugin
    implementation 'de.marcphilipp.nexus-publish:de.marcphilipp.nexus-publish.gradle.plugin:0.4.0'

    //https://docs.sonarqube.org/display/SCAN/Analyzing+with+SonarQube+Scanner+for+Gradle
    //https://plugins.gradle.org/plugin/org.sonarqube
    implementation 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7.0.1622'
}

configurations.all { resolutionStrategy { force 'org.codehaus.groovy:groovy-all:+' } }

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

license {
    headerURI = new java.net.URI('https://blackducksoftware.github.io/integration-resources/project/HEADER.txt')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.projectName = project.name
    ignoreFailures = true
    includes(['**/*.groovy', '**/*.java'])
    excludes(['/src/test/*.groovy',
              'src/test/*.java'])
}

tasks.getByName('build').dependsOn(licenseFormatMain)

nexusStaging {
    packageGroup = 'com.blackducksoftware'
    stagingProfileId = '324d855ef1398'
    username = sonatypeUsername
    password = sonatypePassword
    numberOfRetries = 40
}

nexusPublishing {
    repositories {
        sonatype()
        connectTimeout = Duration.ofMinutes(5)
        clientTimeout = Duration.ofMinutes(5)
    }
}

publishToSonatype.onlyIf {
    !project.version.endsWith('-SNAPSHOT')
}

closeRepository.onlyIf {
    !project.version.endsWith('-SNAPSHOT')
}

releaseRepository.onlyIf {
    !project.version.endsWith('-SNAPSHOT')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = rootProject.name
                description = rootProject.description
                url = "https://www.github.com/blackducksoftware/${rootProject.name}"
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'blackduckoss'
                        name = 'Black Duck OSS'
                        email = 'bdsoss@blackducksoftware.com'
                        organization = 'Black Duck Software, Inc.'
                        organizationUrl = 'http://www.blackducksoftware.com'
                        timezone = 'America/New_York'
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/blackducksoftware/${rootProject.name}.git"
                    developerConnection = "scm:git:git@github.com:blackducksoftware/${rootProject.name}.git"
                    url = "https://www.github.com/blackducksoftware/${rootProject.name}"
                }
            }
        }
    }
}

publish.dependsOn { println "publish will attempt uploading ${project.name}:${project.version} to maven central" }

signing {
    required { gradle.taskGraph.hasTask('publish') }
    sign publishing.publications.mavenJava
}

def artifactoryRepo = artifactorySnapshotRepo
if (!project.version.endsWith('-SNAPSHOT')) {
    artifactoryRepo = artifactoryReleaseRepo
}

println "outside:" + deployArtifactoryUrl
artifactory {
    println "inside:" + deployArtifactoryUrl
    contextUrl = deployArtifactoryUrl
    publish {
        repository {
            repoKey = artifactoryRepo
            username = artifactoryDeployerUsername
            password = artifactoryDeployerPassword
        }
        defaults { publications ('mavenJava') }
    }
    resolve {
        repository {
            repoKey = artifactoryReleaseRepo
        }
    }
}

artifactoryPublish.dependsOn { println "artifactoryPublish will attempt uploading ${project.name}:${project.version} to ${deployArtifactoryUrl}/${artifactoryRepo}" }