plugins {
    id 'groovy'
    id 'maven'
    id 'signing'
    id 'eclipse'
}

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
tasks.withType(GroovyCompile) { options.encoding = 'UTF-8' }

group = 'com.blackducksoftware.integration'
version = '0.0.8'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2/' }
}

dependencies {
    compile gradleApi()

    //https://www.jfrog.com/confluence/display/RTF/Gradle+Artifactory+Plugin
    compile 'org.jfrog.buildinfo:build-info-extractor-gradle:4.5.4'

    //https://github.com/hierynomus/license-gradle-plugin
    compile 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0'

    //https://github.com/kt3k/coveralls-gradle-plugin
    compile 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'

    //https://github.com/Codearte/gradle-nexus-staging-plugin
    compile 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0'
}

configurations.all {
    resolutionStrategy {
        force 'org.codehaus.groovy:groovy-all:2.4.12'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment ->
                signing.signPom(deployment)
            }
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.project {
                name rootProject.name
                description rootProject.description
                url "https://www.github.com/blackducksoftware/${rootProject.name}"
                packaging 'jar'
                scm {
                    connection "scm:git:git://github.com/blackducksoftware/${rootProject.name}.git"
                    developerConnection "scm:git:git@github.com:blackducksoftware/${rootProject.name}.git"
                    url "https://www.github.com/blackducksoftware/${rootProject.name}"
                }
                licenses {
                    license {
                        name 'Apache License 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id 'blackduckoss'
                        name 'Black Duck OSS'
                        email 'bdsoss@blackducksoftware.com'
                        organization 'Black Duck Software, Inc.'
                        organizationUrl 'http://www.blackducksoftware.com'
                        roles { role 'developer' }
                        timezone 'America/New_York'
                    }
                }
            }
        }
    }
}

gradle.taskGraph.whenReady {
    if (gradle.taskGraph.hasTask(":uploadArchives")) {
        println "Uploading ${rootProject.name} version ${version} to maven central."
    }
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}
