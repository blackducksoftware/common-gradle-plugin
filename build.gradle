import java.time.Duration

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'jacoco'
    id 'signing'
    id 'eclipse'
    id 'org.sonarqube' version "${SONARQUBE_VERSION}"
    id 'com.jfrog.artifactory' version "${ARTIFACTORY_VERSION}"
    id 'org.cadixdev.licenser' version "${LICENSE_VERSION}"
    id 'io.codearte.nexus-staging' version "${NEXUS_STAGING_VERSION}"
    id 'de.marcphilipp.nexus-publish' version "${NEXUS_PUBLISH_VERSION}"
}

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
tasks.withType(GroovyCompile) { options.encoding = 'UTF-8' }

group = 'com.synopsys.integration'
version = '2.0.0-SNAPSHOT'
description = 'A common plugin for all integration libraries and solutions to use.'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenLocal()
    maven { url "${project.ext.downloadArtifactoryUrl}/${project.ext.artifactoryReleaseRepo}" }
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2/' }
}

dependencies {
    implementation gradleApi()
    implementation 'org.apache.commons:commons-lang3:3.9'

    //https://www.jfrog.com/confluence/display/RTF/Gradle+Artifactory+Plugin
    //https://github.com/JFrogDev/build-info/tree/master/build-info-extractor-gradle
    implementation "org.jfrog.buildinfo:build-info-extractor-gradle:${ARTIFACTORY_VERSION}"

    //https://github.com/CadixDev/licenser
    implementation "gradle.plugin.org.cadixdev.gradle:licenser:${LICENSE_VERSION}"

    //https://github.com/Codearte/gradle-nexus-staging-plugin
    implementation "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:${NEXUS_STAGING_VERSION}"

    //https://github.com/marcphilipp/nexus-publish-plugin
    implementation "de.marcphilipp.nexus-publish:de.marcphilipp.nexus-publish.gradle.plugin:${NEXUS_PUBLISH_VERSION}"

    //https://docs.sonarqube.org/latest/analysis/scan/sonarscanner-for-gradle/
    //https://plugins.gradle.org/plugin/org.sonarqube
    implementation "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${SONARQUBE_VERSION}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
}

test {
    useJUnitPlatform()
}

configurations.all { resolutionStrategy { force 'org.codehaus.groovy:groovy-all:+' } }

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'black-duck-software'
    }
}

license {
    header = project.file('HEADER.txt')
    properties {
        projectName = project.name
        year = Calendar.getInstance().get(Calendar.YEAR)
    }
    newLine = false
    ignoreFailures = true
    include 'src/main/**/*.java'
    include 'src/main/**/*.groovy'
}

tasks.getByName('build').dependsOn(updateLicenses)

nexusStaging {
    packageGroup = 'com.synopsys'
    stagingProfileId = '4af8bea8300633'
    username = sonatypeUsername
    password = sonatypePassword
    numberOfRetries = 250
    delayBetweenRetriesInMillis = 5000
}

nexusPublishing {
    repositories {
        sonatype()
        connectTimeout = Duration.ofMinutes(10)
        clientTimeout = Duration.ofMinutes(10)
    }
}

publishToSonatype.onlyIf {
    !project.version.endsWith('-SNAPSHOT')
}

closeRepository.onlyIf {
    !project.version.endsWith('-SNAPSHOT')
}

releaseRepository.onlyIf {
    !project.version.endsWith('-SNAPSHOT')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = rootProject.name
                description = rootProject.description
                url = "https://www.github.com/blackducksoftware/${rootProject.name}"
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'blackduckoss'
                        name = 'Synopsys OSS'
                        email = 'oss@synopsys.com'
                        organization = 'Synopsys, Inc.'
                        organizationUrl = 'https://www.synopsys.com'
                        timezone = 'America/New_York'
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/blackducksoftware/${rootProject.name}.git"
                    developerConnection = "scm:git:git@github.com:blackducksoftware/${rootProject.name}.git"
                    url = "https://www.github.com/blackducksoftware/${rootProject.name}"
                }
            }
        }
    }
}

publish.dependsOn { println "publish will attempt uploading ${project.name}:${project.version} to maven central" }

signing {
    required { gradle.taskGraph.hasTask('publish') }
    sign publishing.publications.mavenJava
}

def artifactoryRepo = artifactorySnapshotRepo
if (!project.version.endsWith('-SNAPSHOT')) {
    artifactoryRepo = artifactoryReleaseRepo
}

artifactory {
    contextUrl = deployArtifactoryUrl
    publish {
        repository {
            repoKey = artifactoryRepo
            username = artifactoryDeployerUsername
            password = artifactoryDeployerPassword
        }
        defaults { publications('mavenJava') }
    }
    resolve {
        repository {
            repoKey = artifactoryReleaseRepo
        }
    }
}

artifactoryPublish.dependsOn { println "artifactoryPublish will attempt uploading ${project.name}:${project.version} to ${deployArtifactoryUrl}/${artifactoryRepo}" }
